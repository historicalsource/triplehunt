	.TITLE BEAR 16-DEC-76 TAH
	.ASECT
OBSTK:	.BLKB 10
ORGSTK:	.BLKB 10
HPSTK:	.BLKB 10
VPSTK:	.BLKB 10
VECSTK:	.BLKB 10
TSSTG:	.BLKB 2
TXSTG:	.BLKB 2
TXSTG1:	.BLKB 2
ROARON:	.BLKB 1
RSET:	.BLKB 1
DIRB:	.BLKB 1
RSC:	.BLKB 1
GFG1:	.BLKB 2
TXSTG2:	.BLKB 2
POT:	.BLKB 2				;SELECT POT, CONTAINS 0C10 THRU 0C13
OLDSHT:	.BLKB 2
INWH:	.BLKB 1				;MESSAGE INPUT ADDRESS HOLD
INW1:	.BLKB 1				;LOW ORDER INPUT ADDRESS
OUTWH:	.BLKB 1				;MESSAGE OUTPUT ADDRESS HOLD
OUTW1:	.BLKB 1				;LOW ORDER OUTPUT ADDRESS
VALUE:	.BLKB 1				;HOLDS LAST READ POT VALUE FROM INTERRUPT ROUTINE
TSTWD:	.BLKB 1				;TEST WORD, ONE BIT SET ONLY, USED TO CLEAR
					;OR SET A BIT IN CLRFLG IN NORMAL GUN ROUTINE
					;BIT 0 SET, POT X1; BIT L SET, POT Y1, ETC.
WAIT:	.BLKB 1				;V BLANK WAIT FLAG. ALLOW NO V-BLANK INTERRUPT WHEN NEGATIVE
CLRFLG:	.BLKB 1
GOULCN:	.BLKB 1				;UNUSED
GVTYM:	.BLKB 1
EXT:	.BLKB 2				;&111
EXFG:	.BLKB 2
EXVAL:	.BLKB 2
RTC:	.BLKB 1				;REAL TIME CLOCK
CONCNT:	.BLKB 1
FREE:	.BLKB 1				;ESCAPE FLAG IN GUN ALIGNMENT. IF 0, TEST SWITCH OFF
R0:	.BLKB 1				;GEN. PURPOSE REGISTERS
R1:	.BLKB 1
R2:	.BLKB 1
R3:	.BLKB 1
R4:	.BLKB 1
R5:	.BLKB 1
RORFLG:	.BLKB 1
R8:	.BLKB 1
TIX:	.BLKB 2				;TEMP IX REGISTERS
TIX2:	.BLKB 2
TIX3:	.BLKB 2
TIX4:	.BLKB 2
TIX5:	.BLKB 2
SCRN1:	.BLKB 2				;SHOT 1 OUTPUT LOCATION
TMPPOT:	.BLKB 2
TDEL:	.BLKB 1
ATCNT:	.BLKB 1
RSPEED:	.BLKB 1
SLOT:	.BLKB 1
SCRECH:	.BLKB 1
SHOTS:	.BLKB 1
TRIG:	.BLKB 1				;UNUSED
LRAC:	.BLKB 1				;UNUSED
TYMCY:	.BLKB 1
TYMCNT:	.BLKB 1
TYMSTK:	.BLKB 10
VMSTK:	.BLKB 10
PCSTK:	.BLKB 10
R6:	.BLKB 1
R7:	.BLKB 1
TIMEC:	.BLKB 2
ATT:	.BLKB 1				;ATTRACT MODE FLAG, 0=ATTRACT, MINUS=NO ATTRACT
GUN1:	.BLKB 4
ALST:	.BLKB 2
RAMST:	.BLKB 2
COINS:	.BLKB 1				;COINS INSERTED COUNTER
POTX1:	.BLKB 1
POTY1:	.BLKB 1
DEV1:	.BLKB 2		
RORWRD:	.BLKB 2	
MSD:	.BLKB 1
GAME:	.BLKB 1
CREDIT:	.BLKB 1
TEMPV:	.BLKB 1
HISCR:	.BLKB 3
BIG	=6071			;WINDOW ON FOR 64X64
SMALL	=6070			;WINDOW OFF FOR 32X32
DP	=2000
ORGRAM	=4057
VPRAM	=4000
HPRAM	=4020
PICRAM	=4060
ROAR	=2372				;*ASSIGN ADDRESS OF SOUND LATER
BEAR	=6076
SHOT	=2374
TRIGR	=6110
TYMSET	=6010
GAMSET	=6011
CMODE	=6012
TYMRST	=6013
CRAM	=6040
CN1	=6000
CN2	=6100
TSTSWT	=6110
STLMP	=6062
SKRET	=6064			;CHANGE TO 6064 ON FINAL
CNLKOT	=6066
RAM2EN	=6072
TAPE	=6074
DIAG	=40000
INTWRD	=6060
VBRSET	=6000
SWAP	=117
	.=70000
CK:	.BYTE 	52
TESTOP:	TST	E,VBRSET
	SEI
	TST	E,TYMRST
	TST	E,TAPE
	TST	E,SKRET
	CLRB				;CLEAR SCREEN
	STAB	E,SHOT
	STAB	E,ROAR
	LDX	I,DP+367
	CLRA
1$:	STAA	X,0
	DEX
	BNE	1$
	DECA
	TST	E,TYMRST
	LDX	I,377
2$:	STAA	X,0
	DEX
	DECA
	BNE	2$
	STAA	X,0
	LDX	I,377
	DECA
3$:	CMPA	X,0
	BNE	4$
	DEX
	DECA
	BNE	3$
	CMPA	X,0
	BEQ	5$
4$:	LDAB	I,1
	TSTA
	BPL	6$
	LDAA	I,2
	STAA	E,DP+26
	BRA	5$
6$:	LDAA	I,1
	STAA	E,DP+25
5$:	TST	E,TYMRST
	LDX	I,DP+20
	LDAA	I,24
	STAA	X,0
	LDAA	I,5
	STAA	X,1
	LDAA	I,23
	STAA	X,2
	LDAA	I,24
	STAA	X,3
ROMTST:	LDX	I,CK
	LDAA	X,0			;PROM1
	INX
1$:	SUBA	X,0
	INX
	CPX	I,CK+2000
	BNE	1$
	TSTA
	BEQ	2$
	LDAA	I,3
	INCB
	STAA	E,DP+27
2$:	TST	E,TYMRST
	LDAA	X,0			;PROM2
	INX
3$:	SUBA	X,0
	INX
	CPX	I,CK+4000
	BNE	3$
	TSTA
	BEQ	4$
	LDAA	I,4
	INCB
4$:	TST	E,TYMRST
	STAA	E,DP+30
	LDAA	X,0			;PROM3
	INX
5$:	SUBA	X,0
	INX
	CPX	I,CK+6000
	BNE	5$
	TSTA
	BEQ	6$
	LDAA	I,5
	INCB
	STAA	E,DP+31
6$:	TST	E,TYMRST
	LDAA	X,0			;PROM4
	INX
7$:	SUBA	X,0
	INX
	CPX	I,CK+7777
	BNE	7$
	SUBA	X,0
	TSTA
	BEQ	ROMOVR
	LDAA	I,6
	INCB
	STAA	E,DP+32
ROMOVR:	TST	E,TYMRST
	TSTB
	BEQ	GALN
1$:	TST	E,TYMRST
	LDAA	E,TSTSWT
	BITA	I,100
	BEQ	1$
	JMP	E,PWRUP
GALN:	JSR	E,BEGIN
PWRUP:	LDS	I,377
	LDAA	E,DIAG+1
	CMPA	I,216
	BNE	2$
	JMP	E,DIAG
2$:	LDAA	E,TSTSWT
	BITA	I,100
	BNE	1$
	JMP	E,TESTOP
1$:	TST	E,TYMRST
	JSR	E,INIT
	STAA	E,STLMP			;LAMPS OFF
	LDAA	I,60
	STAA	D,MSD
	STAA	D,R6
	STAA	D,R7
	STAA	D,HISCR
	STAA	D,HISCR+1
	STAA	D,HISCR+2
	STAA	D,TIMEC
	STAA	D,TIMEC+1
ATLPZ:	JSR	E,INIT
	CLR	E,COINS
	CLR	E,CREDIT
	TST	E,TYMRST
	JSR	E,CLEAN
	TST	E,CNLKOT+1			;ACCEPT COINS
	LDAA	I,377
	STAA	D,ATCNT
ATLP:	LDX	I,RORTBL
	STX	D,RORWRD
	TST	E,TAPE
	TST	E,SKRET			;SCREECH OFF
	CLRB
	STAB	E,SHOT
	STAB	E,ROAR
	LDAA D,RTC
	ANDA I,37
	BNE ATLPQ
	LDAA D,GVTYM
	BEQ 1$
	BRA 2$
1$:	LDAB I,14			;GAME OVER
	COM E,GVTYM
	JSR E,DSPLY
	LDAB	I,36
	JSR	E,DSPLY
	BRA	3$
2$:	COM	E,GVTYM
	CLRB
	JSR	E,DSPLY
	LDAB	I,34
	JSR	E,DSPLY
3$:	LDAB	I,2
	JSR	E,DSPLY
	LDAA	E,CMODE
	ANDA	I,300
	BNE	NZCN
CM0:	LDAA	I,61
	CLRB
	STAA	E,DP+305
	STAB	E,DP+313
CLST:	STAA	E,DP+325
	STAB	E,DP+333
	BRA	ATCNCY
NZCN:	CMPA	I,100
	BNE	1$
	LDAA	I,61
	CLRB
	BRA	CLST
1$:	CMPA	I,200
	BNE	CM0
	LDAA	I,61
	CLRB
	STAA	E,DP+305
	STAB	E,DP+313
ATCNCY:	LDAA	D,ATCNT
	BNE	1$
	JMP	E,ATLPZ
1$:	DECA
	STAA	D,ATCNT
ATLPQ:	LDAA	E,TSTSWT		;TEST & ALIGNMENT?
	BITA	I,100			;CHANGE TO 100 LATER
	BNE	ACTIVE
	JMP	E,TESTOP			;DO TEST

ACTIVE:	TST	E,TYMRST
	LDAA	E,GAMSET
	ANDA	I,300
	CMPA	D,GAME
	BEQ	TSTAT
	STAA	D,GAME
	JSR E,INIT
	TST	E,TYMRST
	CLR	E,ATT
TSTAT:	TST	E,GAME
	BEQ	BEARAT
	BPL	1$
	JMP	E,RACONT
1$:	LDAA	D,ATT
	BMI	BRCY
	JSR	E,GOULST
	BRA	GLRCYC
ALOOP:	JSR	E,COININ
	JSR	E,TSCR
	LDX	I,DP+2
	LDAA	D,HISCR
	STAA	X,0
	LDAA	D,HISCR+1
	STAA	X,1
	LDAA	D,HISCR+2
	STAA	X,2
	LDAA	I,60
	STAA	X,3
	LDAB	I,30			;HIGH
	JSR	E,DSPLY
	LDAB	I,32			;SCORE
	JSR	E,DSPLY
	JSR	E,HORGR
	LDAA	D,COINS	
	BNE	COINOP
	JMP	E,ATLP
BEARAT:	LDAA D,ATT
	BMI BRCY
	JSR E,BINIT
RCYC:	LDAA I,200
	STAA D,ATT
	BRA	ALOOP
BRCY:	TST	E,TYMRST
	TST E,VBRSET
	CLR E,WAIT
	CLI
1$:	LDAA	E,CN1
	ANDA	E,CN2
	BMI	2$
	JSR	E,COININ
2$:	TST	E,WAIT
	BEQ 1$		;CHANGE FOR COINS !!!!!!!!!!!!!!!!!!!!!!!!!!!!
	TST	E,TYMRST
	TST	E,GAME
	BEQ	BRATCY
	BMI	RNATCY
	JSR	E,GOULMV
	JSR	E,GOULST
	BRA	ATCYCL
RNATCY:	JSR	E,RACMOT
	JSR	E,RACSET
	BRA	ATCYCL
BRATCY:	JSR E,BMOT
ATCYCL:	JSR E,HORGR
	JMP E,ALOOP

RACONT:	LDAA	D,ATT
	BMI	BRCY
	JSR	E,RACSET
GLRCYC:	TST	E,BEAR
	TST	E,SMALL
	LDAA	I,77
	STAA	D,RSPEED
	BRA	RCYC
COINOP:	LDAA	E,CMODE
	ANDA	I,300		;0=1 CN/1GM
	BNE	MOD1
MOD0:	CLR	E,CREDIT
STCYCL:	TST	E,STLMP+1	;START LAMP ON
	TST	E,CNLKOT	;LOCK OUT COINS
	JSR	E,CLEAN
	LDAB	I,22		;PUSH START
	JSR	E,DSPLY
	JSR	E,TYSCR
	CLR	E,COINS
1$:	TST	E,TYMRST
	LDAA	E,CN1
	BITA	I,100
	BNE	1$
	JMP	E,STGM
MOD1:	CMPA	I,100		;1=2 CN/1GM
	BNE	MOD2
	JSR	E,CLEAN
1$:	LDAA	D,COINS
	CMPA	I,2
	BCC	MOD0
	LDAB	I,24		;INSERT COIN
	JSR	E,DSPLY
	JSR	E,TYSCR
	JSR	E,COININ
	TST	E,TYMRST
	BRA	1$
MOD2:	CMPA	I,200		;2=1 CN/2GM
	BNE	MOD0
	LDAA	I,1
	STAA	D,CREDIT
	BRA	STCYCL
STGM:	JSR	E,CLEAN
	JSR E,INIT
	TST	E,TYMRST
	TST	E,STLMP			;START LAMP OFF
	TST	E,TAPE+1
	CLR	E,ATT
	JSR	E,GTWDS
	TST	E,TYMRST
	LDX	I,DP+360
	STX	D,OLDSHT
	LDAB	E,TYMSET
	ASLB
	ROLB
	ROLB
	ANDB	I,3
	TST	E,GAME
	BMI	TGOTB
	LDX	I,GBRTBL		;BEAR & HOUSE
	BRA	BGLCYC
TGOTB:	LDX	I,RTYMTB		;RACCOON
BGLCYC:	STX	D,TIMEC
	ADDB	D,TIMEC+1
	STAB	D,TIMEC+1
	LDAB	D,TIMEC
	ADCB	I,0
	STAB	D,TIMEC
	LDX	D,TIMEC
	TST	E,GAME
	BEQ BREX
	BMI	RTGOTE
	LDAB	X,10
	LDAA	X,20
	BRA	HREX
BREX:	LDAB	X,4			;HOUSE
	LDAA	X,14			;BEAR
HREX:	STAB	D,EXVAL
	STAA	D,EXVAL+1
	LDAA	X,0
	STAA	D,TYMCY
	STAA	D,TYMCNT
	BRA	TGOTA
RTGOTE:	LDAA	X,0			;RACCOON
	LDAB	X,4
	STAA	D,TIMEC
	STAB	D,TIMEC+1
	LDAA	X,10
	LDAB	X,14
	STAA	D,EXT
	STAB	D,EXT+1
	LDAB	X,20
	STAB	D,EXVAL
	LDAB	X,24
	STAB	D,EXVAL+1
TGOTA:	LDAA	E,TYMRST		;CHECK EXTENDED PLAY
	BMI	3$
	LDAB	I,16			;EX PLAY
	JSR	E,DSPLY
	LDAA	D,EXVAL
	CMPA	I,60
	BNE	4$
	LDAA	D,EXVAL+1
	LDAB	I,60
	STAA	E,DP+46
	STAB	E,DP+47
	STAB	E,DP+50
	BRA	5$
4$:	STAA	E,DP+46
	LDAA	D,EXVAL+1
	STAA	E,DP+47
	LDAB	I,60
	STAB	E,DP+50
	STAB	E,DP+51
5$:	LDAA	I,7
2$:	LDX	I,77777
1$:	TST	E,TYMRST
	DEX
	BNE	1$
	DECA
	BNE	2$
3$:	JSR	E,CLEAN
	TST	E,GAME
	BMI	TCY4
	LDAB	I,71
	STAB	D,TIMEC
	STAB	D,TIMEC+1
TCY4:	LDAA	I,60
	STAA	D,MSD
	STAA	D,R6			;ZERO SCORE
	STAA	D,R7
	JSR	E,TYSCR
	TST	E,GAME
	BEQ	BEAR0
	LDAA	I,77
	STAA	D,RSPEED
	CLR	E,CONCNT
	TST	E,BEAR
	STAA	E,SMALL			;SET SMALL WINDOW
	BRA	CICLE
BEAR0:	JSR E,BINIT
	CLRA
	STAA	E,ROARON
	TST	E,BIG
	TST	E,BEAR+1		;SET FOR BEAR PICTURES
CICLE:	TST	E,TYMRST
	TST	E,VBRSET
CYCLE:	CLR	E,WAIT			;CLEAR FLAG
	CLI
1$:	TST	E,WAIT			;TEST FLAG
	BEQ	1$			;WAIT FOR V-BLANK
	TST	E,TYMRST
	JSR	E,INIT2
	JSR	E,NORM			;GUN
	LDAA	D,GAME
	BEQ	BEAR1
	BMI	RACON1
	JSR	E,GOULMV
	JSR	E,GOULST
	BRA	CONTYN
RACON1: JSR	E,RACMOT
	JSR	E,RACSET
	BRA	CONTYN
BEAR1:	JSR	E,BMOT			;BEAR MOTION
CONTYN:	JSR	E,HORGR
	TST	E,GAME
	BPL	CLKCK
	TST	E,CONCNT
	BMI	1$
	JMP	E,SOUND
1$:	LDAA	E,TYMRST
	BMI	CRDTST
	TST	E,EXFG
	BMI	EXON
CRDTST:	LDAA	E,CMODE
	ANDA	I,300
	CMPA	I,200
	BEQ	1$
2$:	JMP	E,HITST
1$:	LDAA	D,CREDIT
	BEQ	2$
	CLRB
	TST	E,SKRET
	TST	E,TAPE
	STAB	E,SHOT
	STAB	E,ROAR
	JMP	E,MOD0
	.BYTE 0,0,0
	.BYTE	40			;CKSUM 2ND K
EXON:	CLR	E,CONCNT
1$:	LDAA	D,EXT
	LDAB	D,EXT+1
EXCYC:	STAA	D,TIMEC
	STAB	D,TIMEC+1
	LDAA	I,200
	STAA	D,EXVAL
	CLR	E,EXFG
	LDAB	I,20
	JSR	E,DSPLY
	BRA	NEXPLY
CLKCK:	LDAA	D,TYMCNT
1$:	BEQ	CLK0
	DECA
	STAA	D,TYMCNT
	BRA	SOUND
CLK0:	LDAA	D,TYMCY
	STAA	D,TYMCNT
	LDAA	D,TIMEC+1
	DECA
	STAA	D,TIMEC+1
	CMPA	I,57
	BNE	SOUND
	LDAB	D,TIMEC
	CMPB	I,60
	BNE	CLKON
	STAB	D,TIMEC+1
	STAA	E,DP+5
	STAB	E,DP+4
	LDAA	E,TYMRST
	BMI	CRDTST
2$:	TST	E,EXFG
	BMI	1$
	BRA	CRDTST
1$:	LDAA	I,64
	LDAB	I,71
	BRA	EXCYC
CLKON:	DECB
	STAB	D,TIMEC
	LDAA	I,71
	STAA	D,TIMEC+1
SOUND:	LDAA	D,EXVAL
	BPL	NEXPLY
	LDAB	I,20
	JSR	E,DSPLY
NEXPLY:	JSR	E,TYSCR
	LDAA	D,SCRECH
	BEQ	SHTSND

	DEC	E,SCRECH
	BNE	SHTSND
	TST	E,SKRET
SHTSND:	LDAA D,SHOTS
	BEQ NOSHT
	DECA
	CMPA I,14
	BEQ SHTEND
SHTOUT:	STAA D,SHOTS
	STAA E,SHOT
	BRA NOSHT
SHTEND:	LDX D,OLDSHT
	CLR X,0
	BRA SHTOUT
NOSHT:	TST	E,RORFLG
	BPL 	CYCEND
	LDX	D,RORWRD
	LDAA	X,0
	STAA	E,ROAR
	INX
	CPX	I,RORTBL+100
	BEQ	ENDRR
	STX	D,RORWRD
CYCEND:	JMP	E,CYCLE
ENDRR:	LDX	I,RORTBL
	STX	D,RORWRD
	CLR	E,RORFLG
	BRA	CYCEND
HITST:	LDAA	D,MSD
	CMPA	D,HISCR
	BGT	2$
	BNE	1$
	LDAA	D,R6
	CMPA	D,HISCR+1
	BGT	2$
	BNE	1$
	LDAA	D,R7
	CMPA	D,HISCR+2
	BGT	2$
1$:	JMP	E,ATLPZ
2$:	LDAA	D,MSD
	STAA	D,HISCR
	LDAA	D,R6
	STAA	D,HISCR+1
	LDAA	D,R7
	STAA	D,HISCR+2
	BRA	1$

TYSCR:	LDAA	D,TIMEC
	STAA	E,DP+4
	LDAA	D,TIMEC+1
	STAA	E,DP+5
TSCR:	LDAA	D,MSD
	STAA	E,DP+13
	LDAA	D,R6
	STAA	E,DP+14
	LDAA	D,R7
	STAA	E,DP+15
	LDAA	I,60
	STAA	E,DP+16
	RTS
GBRTBL:	.BYTE 23,46,71,114		;30,60,90,&120 SEC
	.BYTE 60,61,61,61,61,63,64,65			;EXVAL FOR BEAR/HOUSE
	.BYTE 70,62,65,70,64,60,60,60		;EXVAL+1 FOR BEAR/HOUSE
RTYMTB:	.BYTE 61,63,64,66,66,62,70,64		;16,32,48,64
	.BYTE 60,61,62,63,70,66,64,62		;16,24,32
	.BYTE 60,61,61,61,71,62,65,70		;EXVAL RAC/EXVAL+1 RAC
RORTBL:	.BYTE 1,1,1,1,1,1,1,1,2,2,2,2,2,2,2,2 
	.BYTE 4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4
	.BYTE 4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4 
	.BYTE 4,4,4,4,4,4,4,4,3,3,3,2,2,1,1,0 
;HAUNTED HOUSE

BORDER: LDAA	X,VPSTK-1	;RETURNS A=0 IF RESET ELSE A=1
	CMPA	I,10
	BCC	BDRBOT

BDCLR:	CLR	X,VPSTK-1
	CLR	X,HPSTK-1
	LDAA	X,VECSTK-1
	ANDA	I,177
	STAA	X,VECSTK-1
	CLRA
	RTS
BDRBOT: CMPA	I,370
	BCC	BDCLR
	LDAA	X,HPSTK-1
	CMPA	I,10
	BCS	BDCLR
	CMPA	I,370
	BCC	BDCLR
	LDAA	I,1
	RTS


GOULST:	LDX	I,4
GOUL0:	LDAA	X,VECSTK-1
	BPL	GOUL2
GOUL1:	DEX
	BNE	GOUL0
	RTS
GOUL2:	JSR	E,RANDOM
	ANDA	I,7
	STAA	D,SLOT
	STX	D,TXSTG		;SAVE OBJ#
GOUL3:	LDX	I,4
GOUL4:	LDAB	X,VECSTK-1
	ANDB	I,7
	CBA
	BEQ	GOUL6
GOUL5:	DEX
	BNE	GOUL4
	BITA	I,4
	BNE	GLSLT
	LDAB	D,GOULCN
	CMPB	I,2
	BEQ	GOUL6
	BRA	GLSLT
GOUL6:	INCA
	ANDA	I,7
	CMPA	D,SLOT
	BNE	GOUL3
	RTS			;NO SLOT OPEN
GLSLT:	LDX	D,TXSTG		;RECLAIM OBJ#
	TAB
	ORAA	I,200		;SET ACTIVE
	STAA	X,VECSTK-1
	JSR	E,RANDOM
	STAB	D,SLOT
	BITB	I,04
	BNE 	GOUL7
	
	LDAA	I,137
	STAA	X,VMSTK-1
	LDAA	I,372		;SELECT GHOST
	STAA	X,OBSTK-1
	INC	E,GOULCN
	BRA	GOUL8
GOUL7:	JSR	E,RANDOM
	ANDA	I,2
	ORAA	I,4
	STAA	X,OBSTK-1	;STORE PICTURE CODE
GOUL8:	LDX	I,GLTBL
	STX	D,TXSTG1
	LDAA	D,TXSTG1+1
	ADDA	D,SLOT
	STAA	D,TXSTG1+1
	LDAA	D,TXSTG1
	ADCA	I,0
	STAA	D,TXSTG1
	LDX	D,TXSTG1
	LDAA	X,0			;VP -> A
	LDAB	X,10			;HP -> A
	LDX	D,TXSTG
	STAA	X,VPSTK-1
	STAB	X,HPSTK-1
	LDAA	D,SLOT
	CMPA	I,6
	BCS	GOUL9
	LDAA	X,VECSTK-1
	ORAA	I,100
	STAA	X,VECSTK-1
	LDAA	X,OBSTK-1
	EORA	I,10
	STAA	X,OBSTK-1
GOUL10:	LDAA	D,RTC
	LSRA
	LSRA
	LSRA
	LSRA
	ANDA	I,7
	STAA	X,TYMSTK-1
GOUL11:	JMP	E,GOUL1
GOUL9:	CMPA	I,4
	BCS	GOUL11
	BRA	GOUL10

GLTBL:	.BYTE	163,254,163,254,150,226,150,210

GLHTBL:	.BYTE	226,226,170,170,135,135,270,270

GOULMV:	LDX	I,4
GLMV0:	LDAA	X,VECSTK-1
	BMI	GLMV1
GLMV2:	DEX
	BNE	GLMV0
	RTS
GLMV1:	TAB
	ANDA	I,4
	BEQ	GLWIND
	LDAA	X,OBSTK-1
	ANDA	I,7
	CMPA	I,6
	BCS	GLMV4			;BAT
	LDAA	D,RTC
	ANDA	I,7
	BNE	GLMV2
	LDAA	I,1
	LDAB	X,VECSTK-1
	LSRB
	BCS	1$
	NEGA
1$:	ADDA	X,VPSTK-1
	STAA	X,VPSTK-1
	JSR	E,RANDOM
	ANDA	I,3
	ORAA	I,1
	BRA	GLMV9
GLMV4:	LDAA	D,RTC
	ANDA	I,7
	BNE	GLMV2
	LDAA	X,TYMSTK-1
	LDAB	X,VECSTK-1
	LSRB
	BCS	1$
	NEGA
1$:	ADDA	X,VPSTK-1
	STAA	X,VPSTK-1
	JSR	E,RANDOM
	ANDA	I,7
GLMV9:	LDAB	X,VECSTK-1
	BITB	I,100
	BNE	1$
	NEGA
1$:	ADDA	X,HPSTK-1
	STAA	X,HPSTK-1
	JSR	E,BORDER
	BEQ	GLMV2
	LDAA	X,OBSTK-1
	EORA	I,1
	STAA	X,OBSTK-1
	BRA	GLMV2
GLWIND:	LDAA	X,VMSTK-1
	BNE	GLMV5
	LDAA	X,VECSTK-1
	ANDA	I,177
	STAA	X,VECSTK-1
	DEC	E,GOULCN
	BRA	GLMV2
GLMV5:	DEC	X,VMSTK-1
	LDAA	D,RTC
	ANDA	I,7
	BNE	GLEXT
	LDAA	X,OBSTK-1
	EORA	I,1
	STAA	X,OBSTK-1
GLEXT:	JMP E,GLMV2

;RACCOON

RACSET:	LDX	I,4		;RESTART INACTIVE OBJS
RAC0:	LDAA	X,VECSTK-1
	BPL	RAC2
RAC1:	DEX
	BNE	RAC0
	RTS
RAC2:	LDAA	X,TYMSTK-1
	BEQ	RAC3		;RESTART THIS OBJ IF CAN
	DEC	X,TYMSTK-1
	BRA	RAC1
RAC3:	JSR	E,RANDOM	;SELECT NEW SLOT #
	ANDA	I,3
	STAA	D,SLOT
	STX	D,TXSTG		;SAVE OBJ #
RAC4:	LDX	I,4
RAC5:	LDAB	X,VECSTK-1
	BPL	RAC6
	ANDB	I,3
	CBA
	BEQ	RAC7
RAC6:	DEX
	BNE	RAC5
	BRA	RGSLT		;FOUND SLOT
RAC7:	LDAB	X,VPSTK-1
	CMPB	I,344
	BCS	SLTMAY
RNXSLT:	INCA
	ANDA	I,3
	CMPA	D,SLOT
	BNE	RAC4
	RTS			;NO SLOT OPEN
SLTMAY:	DEX
	BEQ	RGSLT
	LDAB	X,VECSTK-1
	BPL	SLTMAY
	ANDB	I,3
	CBA
	BNE	SLTMAY
	LDAB	X,VPSTK-1
	CMPB	I,344
	BCS	SLTMAY
	BRA	RNXSLT
RGSLT:	LDX	D,TXSTG		;RECLAIM OBJ #
	TAB
	ORAA	I,200
	STAA	X,VECSTK-1
	ANDA	I,1		;SELECT DIRECTION BIT
	EORA	I,1
	ASLA
	ASLA
	ASLA
	ORAA	I,360
	STAA	X,OBSTK-1	;PICTURE CODE SET
	LDAA	I,370		;VERT POS
	STAA	X,VPSTK-1
	LDX	I,RACTBL
	STX	D,TXSTG1
	ADDB	D,TXSTG1+1
	STAB	D,TXSTG1+1
	LDAB	D,TXSTG1
	ADCB	I,0
	STAB	D,TXSTG1
	LDX	D,TXSTG1
	LDAA	X,0
	LDX	D,TXSTG
	STAA	X,HPSTK-1	;SET HORZ POS
	LDAA	D,RSPEED
	STAA	X,VMSTK-1	;SET MOTION TIME
	LSRA			;DO NEXT OBJ
	STAA	X,PCSTK-1
	BRA	RAC1
RACTBL:	.BYTE	343,216,150,24
	.PAGE
RACMOT:	LDX	I,4		;MOVE OBJS
RAC8:	LDAA	X,VECSTK-1
	BMI	RAC9
RAC11:	DEX
	BNE	RAC8
	RTS
RAC9:	LDAA	X,VMSTK-1
	BEQ	RMOV
	DEC	X,VMSTK-1
	LDAA	X,PCSTK-1
	BEQ	RAC10
	DEC	X,PCSTK-1
	BRA	RAC11
RAC10:	LDAA	X,OBSTK-1
	ANDA	I,370
	STAA	X,OBSTK-1
	BRA	RAC11
RMOV:	LDAA	X,VPSTK-1
	ADDA	I,360		;MOVE UP BY 16
	CMPA	I,32
	BCS	RAC12
	STAA	X,VPSTK-1
	LDAA	X,OBSTK-1
	ANDA	I,370
	ORAA	I,1
	STAA	X,OBSTK-1
	LDAA	D,RSPEED
	STAA	X,VMSTK-1
	LSRA
	STAA	X,PCSTK-1
	BRA	RAC11
RAC12:	CLR	X,HPSTK-1
	CLR	X,VPSTK-1
	LDAA	D,TIMEC
	LDAB	D,TIMEC+1
	DECB
	CMPB	I,57
	BNE	CNTOUT
	INCB
	CMPA	I,60
	BNE	RCNTA
	LDAA	I,200
	STAA	D,CONCNT
	LDAA	I,60
CNTOUT:	STAA	D,TIMEC
	STAB	D,TIMEC+1
	STAA	E,DP+4
	STAB	E,DP+5
	BRA	RAC13
RCNTA:	DECA
	LDAB	I,71
	BRA	CNTOUT
RAC13:	LDAA	D,RTC
	LSRA
	ADDA	D,POTX1
	ADDA	D,POTY1
	ANDA	I,17
	STAA	X,TYMSTK-1
	LDAA	X,VECSTK-1
	ANDA	I,177
	STAA	X,VECSTK-1
	JMP	E,RAC11
RANDOM:	LDAA	D,RTC
	ADCA	D,POTX1
	ADCA	D,POTY1
	ADCA	D,VALUE
	ADCA	D,RSPEED
	RTS

;BEAR MOTION

BINIT:	LDAA	I,317
	STAA	D,VPSTK
	LDAA	I,17
	STAA	D,OBSTK
	STAA D,RSPEED
	LDX	I,HPSTK+7
	LDAA	I,7
1$:	CLR	X,0
	DEX
	DECA
	BNE	1$
	BRA	2$
2$:	LDAA	I,0
	STAA	D,HPSTK
	TST	E,BIG
	TST	E,BEAR+1
	LDAA I,200
	STAA D,VECSTK
	RTS

BMOT:	TST E,BEAR+1
	LDAA	D,RSET		;*RSET=NEG IF BEAR TO BE RESET
	BPL	BMOVE
	CLR	E,HPSTK
	LDAA	D,TDEL
	BEQ	DOX
	DEC	E,TDEL
	RTS
FILR	=74000-.
	.REPT FILR
	.BYTE 0
	.ENDR
	.=74000
	.BYTE 25		;CKSUM 3RD K
DOX:	COM	E,DIRB		;*DIRECTION FLAG LFT=PLUS
	LDAA	D,VPSTK
	CMPA I,317
	BNE 1$
	SUBA I,104
	BRA 2$
1$:	ADDA I,104
2$:	STAA D,VPSTK
	CMPA I,213
	BEQ	SWDW		;DO SMALL WINDOW CHANGE
	TST	E,BIG		;*SET LARGE WINDOW
	BRA	BMOT1
SWDW:	TST	E,SMALL		;*SET SMALL WINDOW
BMOT1:	CLR	E,RSET
	LDAA	D,OBSTK
	EORA	I,10
	STAA	D,OBSTK
BMOVE:	LDAA	D,RSC		;*ROAR SEQUENCE COUNTER
	BEQ	DOMOVE
	CMPA	I,54
	BNE	CNT7
	LDAA	D,OBSTK
	ANDA	I,370
	ORAA	I,4
	STAA	D,OBSTK
	BRA	CNTDEC
CNT7:	CMPA	I,34
	BNE	CNT3
	LDAA	D,OBSTK
	EORA	I,10
	STAA	D,OBSTK
	BRA	CNTDEC
CNT3:	CMPA	I,14
	BNE	CNTDEC
	LDAA	D,OBSTK
	ANDA	I,370
	ORAA	I,5
	STAA	D,OBSTK
CNTDEC:	DEC	E,RSC
	BEQ	CNT0
	RTS
CNT0:	LDAA	D,OBSTK
	ORAA	I,7
	STAA	D,OBSTK
	LDAA	I,200
	STAA	D,VECSTK
	COM	E,DIRB
	RTS			;INSERT HERE ROAR SEQUENCE
DOMOVE:	LDAA	D,HPSTK
	TST	E,DIRB
	BMI	BNG
	LDAB	D,VPSTK
	CMPB	I,317
	BEQ	BSTEP1
	ADDA	I,4		;STEP NUMBER
	CMPA	I,373		;TEST OFF SCREEN
	BCS	BON
REDO:	COM	E,RSET		;SET RESET FLAG
	LDAA	I,17
	STAA	D,TDEL
	RTS
BSTEP1:	ADDA	I,10
	CMPA	I,367
	BCS	BON
	BRA	REDO
BSTEP2:	ADDA	I,370
	CMPA	I,11
	BCS	REDO
	BRA	BON
BNG:	LDAB	D,VPSTK
	CMPB	I,317
	BEQ	BSTEP2
	ADDA	I,374		;STEP NUMBER
	CMPA	I,3		;TEST OFF SCREEN
	BCS	REDO
BON:	LDAB	D,RTC
	ANDB	D,RSPEED
	BNE	BON1
	STAA	D,HPSTK
	LDAA	D,OBSTK
	EORA	I,1
	STAA	D,OBSTK
BON1:	RTS

COININ:	LDAA	E,CN1
	ORAA	E,CN2
	BMI	1$
	RTS
1$:	SEI
	LDAA	I,17
	STAA	D,R4
	STAA	D,R5
CNLP:	LDAA	E,CN1
	BPL	C1NT
	LDAA	D,R4
	BNE	1$
	TST	E,TYMRST
	LDAA	E,CN2
	BPL	CNLP0
	LDAA	D,R5
	BEQ	CNLP0
	BRA	CZCK
1$:	DEC	E,R4
	BNE	CZCK
	INC	E,COINS
	BRA	CNLP0
C1NT:	LDAA	I,17
	STAA	D,R4
CZCK:	LDAA	I,46			;1MSEC DELAY
	STAA	D,R8
1$:	LDAA	E,CN2			;TEST SLAM SWITCH
	BITA	I,100
	BNE	2$
	BRA	ENDCN
2$:	DEC	E,R8
	BNE	1$
	TST	E,TYMRST
CNLP2:	LDAA	E,CN2
	BPL	CZNT
	LDAA	D,R5
	BNE	3$
	TST	E,TYMRST
	LDAA	E,CN1
	BPL	CNLP2
	LDAA	D,R4
	BEQ	CNLP2
	BRA	CNLP
3$:	DEC	E,R5
	BNE	CNLP0
	INC	E,COINS
	BRA	CNLP0
CZNT:	LDAA	I,17
	STAA	D,R5
CNLP0:	LDAA	E,CN1
	ORAA	E,CN2
	BMI	CNLP
ENDCN:	TST	E,VBRSET
	CLI
	RTS

INIT:	SEI
	LDX	I,R6-1			;CLEAR PAGE 0 FROM TO 00
1$:	CLR	X,0
	DEX
	BNE	1$
INIT3:	LDAA	I,01
	STAA	D,TRIG
	CLRA
	STAA	D,COINS
	STAA	D,WAIT
	STAA	D,ATT
	STAA	D,ALST
	LDAA	I,20
	STAA	D,POT+1			;POT SET UP
	STAA	D,RAMST+1
	LDAA	I,07			;INITIALIZE
	LDX	I,ORGSTK
2$:	STAA	X,0			;LD ORGSTK
	INX
	DECA
	BPL	2$
	TST	E,TYMRST
3$:	TST	E,INTWRD		;STR FF TO V POS RAM
	BMI	3$
4$:	TST	E,INTWRD	
	BPL	4$
	LDX	I,HPRAM+17
	CLRA
5$:	STAA	X,0
	DEX
	CPX	I,VPRAM-1		;INITIALIZE VECTOR, H POS, & V POS STACKS
	BNE	5$
	TST	E,TYMRST
6$:	TST	E,INTWRD		;WAIT FOR ANOTHER INTERRUPT
	BMI	6$
7$:	TST	E,INTWRD
	BPL	7$
	LDAA	I,17			;RESET ORG RAM
	LDX	I,ORGRAM
8$:	STAA	X,0
	DEX
	DECA
	BNE	8$
	STAA	X,0
	LDAA	I,370
	LDX	I,PICRAM+17
9$:	STAA	X,0
	DEX
	CPX	I,PICRAM-1
	BNE	9$
	TST	E,VBRSET
	CLI
INIT2:	LDAA	I,14
	STAA	D,POT
	STAA	D,RAMST
	LDAA	I,GUN1			;POINTER TO ALIGNMENT WORDS
	STAA	D,ALST+1
	LDAA	I,02			;SET UP OUTPUT
	STAA	D,FREE			;RESET ESCAPE FLAG
	RTS
DSPLY:	PSHA				;WRITES MESSAGES
	TBA
	LDX	I,MINTBL
	STX	D,INWH
	ADDB	D,INW1
	STAB	D,INW1			;GET TABLE ENTRY LOC
	BCC	1$
	INC	E,INWH
1$:	LDX	D,INWH
	LDX	X,0			;GET ADDR OF INPUT
	STX	D,INWH
	LDX	I,MOUTBL
	STX	D,OUTWH
	ADDA	D,OUTW1
	STAA	D,OUTW1
	BCC	2$
	INC	E,OUTWH
2$:	LDX	D,OUTWH
	LDX	X,0			;GET OUTPUT ADDR
	STX	D,OUTWH
BACK:	LDX	D,INWH
	LDAA	X,0			;GET FIRST CHAR
	INX
	STX	D,INWH
	LDAB	I,100
	CBA
	BEQ	DONT
	LDX	D,OUTWH
	STAA	X,0			;OUTPUT IT
	INX
	STX	D,OUTWH
	BRA	BACK
DONT:	PULA
	RTS

; CLEAR SCREEN
CLEAN:	STX	D,TIX2			;IF CALLED HERE, WILL CLEAN WHOLE SCREEN
	LDX	I,DP+370
	STX	D,R2
	LDX	I,DP-1
	CLRA				;CLEAR PARTIAL DEPENDING ON IX & R2, R3
1$:	INX
	STAA	X,0
	CPX	D,R2
	BNE	1$
	LDX	D,TIX2
	RTS
FRAME:	TST	E,TYMRST
	LDAB	E,INTWRD		;WAIT FOR END OF PRESENT FRAME
	BPL	FRAME
	TST	X,0			;TURN ON LATCH
1$:	LDAB	E,INTWRD		;WAIT FOR ONE FRAME AT LEAST
	BMI	1$			;HIGH OR FALLING
2$:	LDAB	E,INTWRD
	BPL	2$			;WAIT FOR RISING
	LDAB	X,0			;GOT VALUE
	LDX	D,ALST
	STAB	X,0			;STORE POT'S VALUE
	INX
	STX	D,ALST
	LDAB	I,01
	ANDB	D,POT+1			;DONE IF ZERO
	RTS
BEGIN:	JSR	E,INIT			;START GUN ALIGNMENT
	SEI
	COM	E,ATT
	BSR	CLEAN
	CLRB
	JSR	E,DSPLY
	LDAB	I,10			;PRINT INSTRUCTIONS
	JSR	E,DSPLY
	LDAB	I,40			;TEST OK
	JSR	E,DSPLY
	LDAA	I,51
	LDX	I,DP
FIRST:	JSR	E,WAIT2			;WAIT FOR TRIGGER
	TST	E,FREE			;ESCAPE IF SWITCH OFF
	BNE	PAST
HELP:	BSR	CLEAN
	CLRB
	JMP	E,INIT3
PAST:	LDX	D,POT			;AT START = 0C10 POTX1 (HORZ)
	BSR	FRAME
	BNE	SECOND
	INC	E,POT+1
	BRA	FIRST			;DO Y
SECOND:	DEC	E,POT+1		;POINT BACK TO X AGAIN
	BSR	CLEAN
	JSR	E,RELSE			;WAIT FOR RELEASE
	BSR	CLEAN
	LDAB	I,10
	JSR	E,DSPLY
	LDAA	I,52
	LDX	I,DP+357
	JSR	E,WAIT2
	TST	E,FREE			;ESCAPE IF SWITCH OFF
	BEQ	HELP
NEXT:	LDX	D,POT
	BSR	FRAME
	BNE	ENDUP
	INC	E,POT+1
	BRA	NEXT
ENDUP:	TST	E,TYMRST
	JSR	E,CLEAN
; DIVISOR CALCULATION ROUTINE
	LDX	I,DEV1
	STX	D,TIX			;SAVE STORAGE POINTER
	LDX	I,GUN1			;POINT TO LOW FIRST
	LDAB	I,02			;COUNTER
CONTIN:	LDAA	X,2			;HIGH WORD
	SUBA	X,0			;GET DIFFERENCE(DELTA)
	INX				;POINT TO Y WORD NOW
	STX	D,TIX2
	LDX	D,TIX			;GET SAVE LOCATION
	STAA	X,0			;SAVE DELTA
	INX
	STX	D,TIX			;UPDATE & SAVE
	LDX	D,TIX2			;GET OLD ONE BACK
	DECB
	BNE	CONTIN
	LDAB	I,6
	JSR	E,DSPLY			;END OF TEST
	JSR	E,STRM2
	SEI
	LDX	I,GUN1
	LDAB	I,4
1$:	LDAA	X,0
	STAA	X,TEMPV-GUN1
	INX
	DECB
	BNE	1$
	JSR	E,GTWDS
	LDX	I,GUN1
	LDAB	I,4
2$:	LDAA	X,TEMPV-GUN1
	CMPA	X,0
	BNE	3$
	INX
	DECB
	BNE	2$
	BRA	WTOFF
3$:	LDAB	I,26			;TEST G
	JSR	E,DSPLY
	TST	E,TYMRST
	LDX	I,GUN1
	LDAB	I,4
4$:	LDAA	X,TEMPV-GUN1
	STAA	X,0
	INX
	DECB
	BNE	4$
WTOFF:	TST	E,TYMRST
	TST	E,VBRSET
	CLR	E,WAIT
	CLI
1$:	TST	E,WAIT
	BEQ	1$
	JSR	E,NORM
	LDAA	D,SHOTS
	BEQ	NOST
	DECA
	CMPA	I,14
	BEQ	STEND
STOUT:	STAA	D,SHOTS
	STAA	E,SHOT
	BRA	NOST
STEND:	LDX	D,OLDSHT
	CLR	X,0
	BRA	STOUT
NOST:	LDAA	D,SCRECH
	BEQ	2$
	DEC	E,SCRECH
	BNE	BEND
	TST	E,SKRET
	TST	E,STLMP
2$:	LDAA	E,CN1
	ORAA	E,CN2
	BITA	I,200
	BNE	SKR
	LDAA	E,CN1
	ANDA	E,CN2
	BITA	I,100
	BEQ	SKR
	BRA	BEND
SKR:	LDAA	I,7
	STAA	D,SCRECH
	STAA	E,STLMP+1
	TST	E,SKRET+1
BEND:	LDAA	E,TSTSWT		;WAIT FOR OFF
	BITA	I,100			;CHANGE TO 100 ON FINAL0
	BEQ	WTOFF
	TST	E,TYMRST
	JSR	E,CLEAN
	TST	E,VBRSET
	CLI
	RTS
	;RETURN ZERO IN A IF TRIG. NOT PULLED
WHICH:	TST	E,TYMRST
	LDAB	E,TRIGR
	BPL	1$
	CLRB
	RTS
1$:	LDAB	I,1
	RTS
RELSE:	LDAB	I,04
	JSR	E,DSPLY
REL:	BSR	WHICH
	BNE	REL
	RTS
WAIT2:	LDAB	E,TSTSWT
	BITB	I,100			;CHANGE TO 100 ON FINAL
	BEQ	WAITT
	CLR	E,FREE
	RTS
WAITT:	CLI
	LDAB	D,RTC
	BITB	I,20
	BEQ	1$
	CLRB
	STAB	X,0
	BRA	2$
1$:	STAA	X,0
2$:	BSR	WHICH
	BEQ	WAIT2			;WAIT FOR PULL
	RTS
	;NORMAL GUN ROUTINE
NORM:	LDAA	D,TMPPOT+1		;GET POT #
	PSHA
	ANDA	I,03			;SET UP TEST WORD BIT
	LDAB	I,01
1$:	DECA				;ROTATE BIT LEFT ACCORDING TO A'S 2 LSB'S
	BLT	PASS
	ASLB
	BRA	1$
PASS:	STAB	D,TSTWD			;SAVE TEST WORD
	PULA
	TAB				;COPY SELECT WORD & LOAD INTO TEMP IX REG
	ANDA	I,03
	STAA	D,TIX2+1
	CLR	E,TIX2			;HAS 0,1
	LDX 	D,TIX2
	LDAA	X,GUN1+2		;GET POT'S HIGH SCREEN LIMIT
	CMPA	D,VALUE
	BCS	CLRS			;CLEAR OFF SHOT?
	LDAA	D,VALUE			;GET POT'S LOW SCREEN LIMIT
	CMPA	X,GUN1
	BLS	CLRS			;CLEAR OFF SHOT?
	LDAA	D,TSTWD			;INDICATE ON SCREEN
	COMA
	ANDA	D,CLRFLG
	STAA	D,CLRFLG		;CLEAR BIT
	LDAA	D,VALUE
	SUBA	X,GUN1			;LOW LIMIT - POT VALUE
	CLRB
	STAA	X,POTX1
	LSRA
	RORB
	LSRA
	RORB
	LSRA
	RORB
	LSRA
	RORB
	TST	E,TIX2+1
	BNE	1$
	SUBB	X,POTX1
	SBCA	I,0			;(READ-L)15
1$:	CLR	X,POTX1
DIVIDE:	SUBB	X,DEV1
	SBCA	I,0
	BCS DID
	INC	X,POTX1
	BRA	DIVIDE
CLRS:	LDAA	D,CLRFLG		;INDICATE OFF SCREEN
	ORAA	D,TSTWD			;SET BIT
	STAA	D,CLRFLG

	BRA	DD
DID:	ADDB	X,DEV1
	ASLB
	CMPB	X,DEV1
	BCS	1$
	INC	X,POTX1
1$:	LDAA	D,POTX1+1		;GET Y VALUE ON THIS GUN
	CMPA	I,17
	BCC	CLRS
	ASLA				;SHIFT TO UPPER 4 BITS
	ASLA
	ASLA
	ASLA
	ADDA	D,POTX1			;ADD LOWER 4 BITS
	STAA	D,SCRN1+1		;SAVE ADDRESS
	LDAA	I,4
	STAA	D,SCRN1

	;SHOT DISPLAY ROUTINE
	BRA	DD
FILR	=76000-.
	.REPT FILR
	.BYTE	0
	.ENDR
	.=76000
	.BYTE	143			;CKSUM 4TH K
DD:	LDAA	E,TRIGR			;READ SWITCHES
	BMI	CHKTWO			;PLAYER 1 NOT PULLED CHECK
	LDAB	D,GFG1
	BNE	BYE
	ORAB	I,1
	STAB	D,GFG1
	LDAB	D,CLRFLG		;CHECK FOR OFF SCREEN
	BITB	I,3			;IF EITHER BIT SET, SHOT IS OFF SCREEN
	BNE	DOSHS
	LDX	D,OLDSHT
	CLR	X,0
	LDX	D,SCRN1			;LOAD OUTPUT1 ADDRESS
	LDAB	I,77			;LOAD SMALL WHITE SHOT
	STAB	X,0
	STX	D,OLDSHT
DOSHS:	LDAB	I,20
	STAB	D,SHOTS
	BRA	BYE
CHKTWO:	CLR	E,GFG1
BYE:	LDX	D,TMPPOT		;GET NEXT POT SELECT
	STX	D,POT			;PUT IN POT
	RTS
	.PAGE
HORGR:	CLR	E,TXSTG2
HR:	LDAA	I,377
	STAA	D,SWAP			;SET SWAP FLAG
	LDX	I,10
HR0:	DEX
	LDAA	X,ORGSTK		;GET 1ST ORG OBJ NUMBER
	STX	D,TXSTG
	STAA	D,TXSTG2+1
	LDX	D,TXSTG2
	LDAB	X,HPSTK			;GET HPOS OF 1ST ORG OBJ
	LDX	D,TXSTG
	DEX
	LDAA	X,ORGSTK		;GET NEXT ORG OBJ NUMBER
	STAA	D,TXSTG2+1
	LDX	D,TXSTG2
	LDAA	X,HPSTK			;GET HPOS OF NEXT ORG OBJ
	LDX	D,TXSTG
	BNE	CKSWP			;ARE 8 DONE
	TST	E,SWAP			;WERE THERE ANY SWAPS
	BPL	HR			;IF SWAP IS CLR, RUN AGAIN
	TST	E,VBRSET
	RTS
CKSWP:	SBA				;COMPARE H POSITIONS
	BLS	HR0			;IF GREATER, THEN SWAP
	CLR	E,SWAP			;CLR SWAP FLAG
	LDAA	X,ORGSTK
	DEX
	LDAB	X,ORGSTK
	STAA	X,ORGSTK
	INX
	STAB	X,ORGSTK
	BRA	HR0
	LDX	D,ALST			;GET TEMP ADDR
	STAA	X,0			;PUT IN TEMP
	INX
	STX	D,ALST			;UPDATE
	RTS
; SAVE WORDS
STRM2:	SEI
	LDAA	I,14
	STAA	D,TIX4
	LDAA	I,4
	STAA	D,R8
	LDX	I,CRAM
	STX	D,TIX5
	LDX	I,GUN1
1$:	BSR	RYTE
	LDX	D,TIX3
	DEC	E,R8
	BNE	1$
	TST	E,CRAM+14
	TST	E,VBRSET
	CLI
	RTS
RYTE:	TST	E,TYMRST
	LDAA	X,0
	COMA
	TAB
	ANDA	I,17
	LSRB
	LSRB
	LSRB
	LSRB
	ORAA	I,20
	ORAB	I,20
	INX
	STX	D,TIX3
	STAA	D,TIX4+1
	LDX	D,TIX4
	TST	X,0
	LDX	D,TIX5
	TST	X,0
	TST	E,RAM2EN+1
	TST	E,RAM2EN
	INX
	STX	D,TIX5
	STAB	D,TIX4+1
	LDX	D,TIX4
	TST	X,0
	LDX	D,TIX5
	TST	X,0
	TST	E,RAM2EN+1
	TST	E,RAM2EN
	INX
	STX	D,TIX5
	RTS
;GET WORDS FOR GUN ALIGNMENT
GTWDS:	SEI
	LDAB	I,4
	LDX	I,GUN1+3
	STX	D,TIX4
	LDX	I,CRAM+7
1$:	BSR	REED
	LDX	D,TIX3
	DECB
	BNE	1$
	LDX	I,DEV1
	STX	D,TIX
	LDX	I,GUN1
	LDAB	I,2
2$:	LDAA	X,2
	SUBA	X,0
	INX
	STX	D,TIX2
	LDX	D,TIX
	STAA	X,0
	INX
	STX	D,TIX
	LDX	D,TIX2
	DECB
	BNE	2$
	TST	E,CRAM+14
	TST	E,VBRSET
	CLI
	RTS
REED:	TST	E,TYMRST
	LDAA	X,0
	LDAA	X,0
	ASLA
	ASLA
	ASLA
	ASLA
	STAA	D,GUN1
	DEX
	LDAA	X,0
	LDAA	X,0
	ANDA	I,17
	ORAA	D,GUN1
	DEX
	STX	D,TIX3
	LDX	D,TIX4
	STAA	X,0
	DEX
	STX	D,TIX4
	RTS
;TEMPORARY INTERRUPT ROUTINE
INTP:	LDAB	E,INTWRD
	BMI	1$			;TEST FOR V-BLANK INTERRUPT
	JMP	E,HIT			;SPACE FOR HIT ROUTINE
1$:	TST	E,WAIT
	BEQ	2$
	INC	E,RTC			;INC REAL TIME CLOCK FOR ATTRACT
	LDAB	E,VBRSET
	RTI
2$:	LDX	D,POT
	LDAA	X,0			;READ LAST POT
	STAA	D,VALUE
	LDAB	D,POT+1
	INCB
	ANDB	I,21			;IF 2, GO BACK TO ZERO
	STAB	D,TMPPOT+1
	LDAB	D,POT
	STAB	D,TMPPOT
	STS	D,TSSTG
	INC	E,RTC
	LDS	I,OBSTK-1		;PICTURE CODES
	LDAB	I,10
	LDX	I,PICRAM
3$:	PULA
	STAA	X,0
	INX
	DECB
	BNE	3$
	LDAB	I,10			;TRANSFER ORGSTK TO ORGRAM
	LDX	I,ORGRAM-7
4$:	PULA
	COMA
	STAA	X,0
	INX
	DECB
	BNE	4$
	LDX	I,HPRAM
	LDAB	I,10
5$:	PULA
	STAA	X,0
	INX
	DECB
	BNE	5$
	LDX	I,VPRAM
	LDAB	I,10
6$:	PULA
	STAA	X,0
	INX
	DECB
	BNE	6$
	LDS	D,TSSTG
	LDX	I,HPRAM+10
	LDAB	I,10
	CLRA
7$:	STAA	X,0
	INX
	DECB
	BNE	7$
	COM	E,WAIT			;INDICATE INTERRUPT DONE
EXIT:	TST	E,VBRSET
	RTI
HIT:	ANDB	I,17
	STAB	D,R1
	CLR	E,R0
	LDX	D,R0
	LDAA	X,VECSTK
	BPL	EXIT
	TST	E,GAME
	BNE	1$
	JMP	E,BEARH
1$:	BMI	RACONH
	LDAA	X,OBSTK		;HAUNTED HOUSE
	ANDA	I,7
	TAB
	ANDB	I,6
	CMPB	I,2		;GHOST?
	BEQ	SCRG
	CMPA	I,6
	BCS	SCRBAT
	LDAA	I,4
	BRA	SCORH
SCRBAT:	LDAA	I,6
	BRA	SCORH
SCRG:	DEC	E,GOULCN
	LDAA	I,2
SCORH:	JSR	E,SCRUP
	CMPB	D,EXVAL+1
	BNE	SCRALL
	CMPA	D,EXVAL
	BNE	SCRALL
	LDAA	I,200
	STAA	D,EXFG
SCRALL:	LDAA	I,17
	STAA	D,SCRECH
	TST	E,SKRET+1
	LDAA	X,VECSTK
	ANDA	I,177
	STAA	X,VECSTK
	CLR	X,HPSTK
	JMP	E,EXIT

RACONH:	LDAA	I,2
	JSR	E,SCRUP
	CMPB	D,EXVAL+1
	BNE	1$
	CMPA	D,EXVAL
	BNE	1$
	LDAA	I,200
	STAA	D,EXFG
1$:	ANDB	I,17
	LDX	I,RHTTBL
	STX	D,R2
	ADDB	D,R3
	STAB	D,R3
	LDAB	D,R2
	ADCB	I,0
	STAB	D,R2
	LDX	D,R2
	LDAA	X,0
	ANDA	D,RSPEED
	STAA	D,RSPEED
	LDX	D,OLDSHT
	CLR	X,0
	LDX	D,R0
	BRA	SCRALL
RHTTBL:	.BYTE 77,37,37,37,37,17,17,17,17,7
BEARH:	TST	E,RSC
	BEQ	1$
	JMP	E,EXIT
1$:	LDAA	D,VECSTK
	BMI	2$
	JMP	E,EXIT
2$:	LDAA	I,74
	STAA	D,RSC
	LDAA	D,OBSTK
	ANDA	I,370
	ORAA	I,5
	STAA	D,OBSTK
	LDAA	D,VPSTK
	CMPA I,213
	BEQ BRMOR
	LDAA	I,2
BSCR:	BSR	SCRUP
	BRA	SCRDNB
BRMOR:	LDAA	I,4
	BRA	BSCR
SCRDNB:	CMPB	D,EXVAL+1
	BNE	1$
	CMPA	D,EXVAL
	BNE	1$
	LDAA	I,200
	STAA	D,EXFG
1$:	LDAA	I,200
	STAA	D,RORFLG
	LDX	I,RORTBL
	STX	D,RORWRD
	ANDB	I,17
	LDX	I, BHTBL
	STX	D,R2
	ADDB	D,R3
	STAB	D,R3
	LDAB	D,R2
	ADCB	I,0
	STAB	D,R2
	LDX	D,R2
	LDAA	X,0
	ANDA	D,RSPEED
	STAA	D,RSPEED
	JMP	E,EXIT

BHTBL:	.BYTE 17,17,7,7,7,3,3,3,3,3
SCRUP:	LDAB	D,R6
	ADDA	D,R7
	CMPA	I,72
	BCS	SROK
	SUBA	I,12
	INCB
	CMPB	I,72
	BCC	SRNOK
SROK:	STAA	D,R7
	STAB	D,R6
	LDAA	D,MSD
	RTS
SRNOK:	SUBB	I,12
	STAB	D,R6
	STAA	D,R7
	LDAA	D,MSD
	INCA
	CMPA	I,72
	BCS	1$
	LDAA	I,60
1$:	STAA	D,MSD
	LDAB	D,R6
	RTS

MINTBL:	.WORD	MES0				;0
	.WORD	MES2				;2
	.WORD	MES4				;4
	.WORD	MES6				;6
	.WORD	MES10				;10
	.WORD	MES10				;12
	.WORD	MES14				;14
	.WORD	MES16				;16
	.WORD	MES20				;20
	.WORD	MES22				;22
	.WORD	MES24				;24
	.WORD	MES26				;26
	.WORD	MES30
	.WORD	MES32
	.WORD	MES34
	.WORD	MES36
	.WORD	MES40				;40
MOUTBL:	.WORD	DP+70				;0
	.WORD	DP+305				;2
	.WORD	DP+5				;4
	.WORD	DP+2				;6
	.WORD	DP+2				;10
	.WORD	DP+2				;12
	.WORD	DP+70				;14
	.WORD	DP+2				;16
	.WORD	DP+322				;20
	.WORD	DP+23				;22
	.WORD	DP+23				;24
	.WORD	DP+43				;26
	.WORD	DP+22				;30
	.WORD	DP+42				;32
	.WORD	DP+110				;34
	.WORD	DP+110				;36
	.WORD	DP+104				;40
MES0:	.BYTE 0,0,0,0,100
MES34:	.BYTE 0,0,0,0,100
MES2:	.BYTE	62,0,3,17,11,16,23,0,0,0,0,0,0,0
	.BYTE	0,0,62,0,7,1,15,5,23,100
;AIM GUN AT UPPER LEFT AND PULL TRIGGER
MES10:	.BYTE	0,0,1,11,15,0,7,25,16,0,1,24,0,0
	.BYTE	0,0,0,0,0,23,21,25,1,22,5,0,0,0,0,0
	.BYTE	0,0,0,0,1,16,4,0,6,11,22,5,100
;RELEASE TRIGGER
MES4:	.BYTE	22,5,14,5,1,23,5,0,0,0,0,0,0,0,0,0
	.BYTE	24,22,11,7,7,5,22,100
;END OF TEST TURN OFF SWITCH
MES6:	.BYTE	5,16,4,0,17,6,0,24,5,23,24,0,0,0,0,0
	.BYTE	23,24,1,22,24,0,7,1,15,5,100
	;GAME
MES14:	.BYTE	7,1,15,5,100
	;OVER
MES36:	.BYTE	17,26,5,22,100
MES16:	.BYTE	5,30,24,5,16,4,5,4,0,20,14,1,31,0,0,0,6,17,22,0,23,3,17
	.BYTE	22,5,0,17,6,100
	;EXTENDED PLAY
MES20:	.BYTE	5,30,24,5,16,4,5,4,0,20,14,1,31,100
	;PUSH START
MES22:	.BYTE	20,25,23,10,0,23,24,1,22,24,100
	;INSERT COIN
MES24:	.BYTE	11,16,23,5,22,24,0,3,17,11,16,100
	;TEST	G
MES26:	.BYTE	24,5,23,24,0,0,7,100
	;HIGH
MES30:	.BYTE 10,11,7,10,100
	;SCORE
MES32:	.BYTE 23,3,17,22,5,100
	;TEST OK
MES40:	.BYTE 24,5,23,24,0,17,13,100
FILR	=77770-.
	.REPT	FILR
	.BYTE	0
	.ENDR
	.=77770
	.WORD	INTP
	.WORD	PWRUP
	.WORD	PWRUP
	.WORD	PWRUP
	.END
                                                                                                                                                                                                                                                                                                                                                                               